apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = '0.0.1'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "0.01"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:recyclerview-v7:24.2.1'
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

group = 'com.xjy.jymultipleadapter'
install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId 'com.xjy.jymultipleadapter' //自己定义的组名
            artifactId 'library'

            name 'simpleslider'
            description 'A simple adapter allows you to easily use.'
            url 'https://github.com/JYXie233/JYMultipleAdapter'
            inceptionYear '2016'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/JYXie233/JYMultipleAdapter.git'
                url 'https://github.com/JYXie233/JYMultipleAdapter'

            }
            developers {
                developer {
                    name 'jyxie'
                    email 'xietom96@gmail.com'
                }
            }
        }

    }
}

// Bintray

//获取local.propertes的信息
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publish = true
    configurations = ['archives']
    pkg {
        //填入 bintray 上对应的 package 信息
        repo = 'maven'
        name = 'JYMultipleAdapter'
        vcsUrl = 'https://github.com/JYXie233/JYMultipleAdapter.git'
        websiteUrl = 'https://github.com/JYXie233/JYMultipleAdapter'
        licenses = ['Apache-2.0']
        issueTrackerUrl = 'https://github.com/JYXie233/JYMultipleAdapter/issues'
        publicDownloadNumbers = true
        version {
            name = '0.0.1'
            desc = 'JYMultipleAdapter release'
            vcsTag = '1.0.0'
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task findConventions << {
    println project.getConvention()
}